resource "local_file" "inventory_outfile" {
  filename        = "${path.module}/../ansible/inventory.yml"
  file_permission = "0644"
  content = yamlencode({
    # HCL representation of the expected YAML for ansible inventory
    all = {
      vars = {
        # use the bastion as SSH jump host during ansible tasks (through "user_in_bastion@bastion_public_ip")
        ansible_ssh_common_args = "-J ${var.bastion_username}@${hcloud_server.bastion.ipv4_address}"
        # encryption key for consul gossip
        consul_gossip_encrypt_key = random_id.consul_gossip_encrypt_key.b64_std
        # encryption key for nomad gossip
        nomad_gossip_encrypt_key = random_id.nomad_gossip_encrypt_key.b64_std
        # consul ACL token to bootstrap the ACL system (uses 'global-management' policy)
        # this will be used the samw way as the token generated by using 'consul acl bootstrap'
        consul_acl_bootstrap_token = random_uuid.consul_acl_bootstrap_token.result
      }
      children = {
        servers = { # group name
          hosts = local.server_hosts
        }
        clients = {
          hosts = local.client_hosts
        }
        loadbalancers = {
          hosts = local.loadbalancer_hosts
        }
      }
    }
  })
}
